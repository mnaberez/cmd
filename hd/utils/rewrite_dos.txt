;rewrite_dos.prg ==1c01==
  100 l=l+1:ifl=1thenf=abs(peek(65533)=255):poke251,cs:rem create sys flag
  101 ifl<>1then180
  102 :
  103 iff=1then120
  104 ifcs<>0thenpoke45,peek(174):poke46,peek(175)
  110 clr:poke51,00:poke55,00:poke52,22:poke56,22:l=1:f=0
  112 :
  120 dl=peek(186):poke250,f:ha=5632:ta=2*4096:iff=1thenta=4*4096
  130 cs=peek(251)
  140 iff=0thenml=peek(46)*256+peek(45)-147
  150 iff=1thenml=peek(4625)*256+peek(4624)-147
  160 iff=0thenload"system header",dl,1
  170 iff=1thenbank15:bload"system header",u(dl):l=l+1
  172 :
  180 ifl=2andf=0thenload"hdos v?.??",dl,1
  190 ifl=2andf=1thenbload"hdos v?.??",u(dl),p(ta):l=l+1
  200 ifl=3then230
  210 ifl=4then510
  220 :
  230 iff=1thenbank15
  240 ifpeek(57513)=120then@p0
  250 td=30:open15,td,15:print
  260 oi=0:gosub 870
  270 :
  280 ifcs=1then440:rem create sys flag
  290 :
  300 print"{clr}{down}rewrite dos v1.10"
  310 print"{down}this program installs a new hd-dos and"
  320 print"geos/hd driver onto your hd without"
  330 print"destroying any data on the drive."
  340 print"{down}important: this program must be run"
  350 print"from a floppy disk that contains the"
  360 print"files 'system header' 'hdos v?.??' and"
  370 print"'geos/hd v?.??'."
  380 print"{down}to continue, hold down 'write protect',"
  390 print"while you press and release 'reset'."
  400 print"{down}then, clear write protect (if necessary)"
  410 print"and press <return> to rewrite dos"
  420 geta$:ifa$<>chr$(13)then420
  430 :
  440 lb=00:hb=ha/256:print:print
  450 bl=4:bh=0:bc=1:print"writing system header":print:gosub600
  460 :
  470 oi=00:gosub870:lb=00:hb=ta/256:print:print"writing main o.s.":gosub580
  480 iff=0thenload "geos/hd v?.??",dl,1
  490 iff=1thenbload "geos/hd v?.??",u(dl),p(ta)
  500 :
  510 oi=64:gosub870:print:print"writing geos o.s.":gosub580
  520 print:print"press reset on hd to reboot dos"
  530 clr:ifpeek(57513)=120then@p1
  540 close15
  550 end
  560 :
  570 :
  580 print:bl=peek(ha+oi+3):bh=peek(ha+oi+2):bc=peek(ha+oi+1):gosub 600:return
  590 :
  600 poke 251,lb: poke 252,hb:print
  610 co=144
  620 poke 186,td
  630 :
  640 for i=1 to bc
  650 :
  660 for j=1 to 8
  670 sys ml+3
  680 next j
  690 :
  700 gosub 770
  710 bl=bl+1:ifbl=256thenbl=0:bh=bh+1
  720 :
  730 print"{up}"i:next i
  740 return
  750 :
  760 :
  770 ec=10
  780 print#15,"m-w"+chr$(08)chr$(00)chr$(2)chr$(bh)chr$(bl)
  790 print#15,"m-w"+chr$(00)chr$(00)chr$(1)chr$(co)
  800 print#15,"m-r"+chr$(00)chr$(00)chr$(1)
  810 get#15,a$:ifa$=""thena$=chr$(0)
  820 ifa$>chr$(127)then 800
  830 ifa$<chr$(2)  then return
  840 ec=ec-1:ifec>0then 790
  850 print"write error"asc(a$):close15:end
  860 :
  870 sys ml :ifpeek(251)<>peek(ha+oi+5)then 890
  880 ifpeek(252)=peek(ha+oi+4)then return
  890 print"{down}checksum error!"
  900 print"the 'hdos' and/or 'geos/hd' files may"
  910 print"be corrupt, or you may be using"
  920 print"an outdated 'system header' file."
  930 close15:end
